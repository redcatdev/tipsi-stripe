apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION             = 28
def DEFAULT_TARGET_SDK_VERSION              = 28
def DEFAULT_BUILD_TOOLS_VERSION             = '28.0.3'
def DEFAULT_GOOGLE_PLAY_SERVICES_VERSION    = '16.1.0'
def DEFAULT_FIREBASE_MESSAGING_VERSION      = '16.0.8'
def SUPPORT_LIB_VERSION                     = '28.0.0'


android {
  compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
  buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    manifestPlaceholders = [
            tipsiStripeRedirectScheme: "example"
    ]
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  def firebaseVersion = project.hasProperty('firebaseVersion') ? project.firebaseVersion : DEFAULT_FIREBASE_MESSAGING_VERSION
  def googlePlayServicesVersion = rootProject.hasProperty('googlePlayServicesVersion')  ? rootProject.googlePlayServicesVersion : DEFAULT_GOOGLE_PLAY_SERVICES_VERSION
  def supportLibVersion = rootProject.hasProperty('supportLibVersion') ? rootProject.supportLibVersion: SUPPORT_LIB_VERSION

  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'com.facebook.react:react-native:+'
  implementation "com.android.support:support-v4:$supportLibVersion"
  implementation "com.android.support:appcompat-v7:$supportLibVersion"

  implementation "com.google.android.gms:play-services-wallet:16.0.0"
  implementation "com.google.firebase:firebase-core:$firebaseVersion"
  implementation 'com.stripe:stripe-android:8.7.0'
  implementation 'com.github.tipsi:CreditCardEntry:1.5.1'

  // for alipay
  compile files('libs/alipaySdk-20180601.jar')
}

repositories {
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}
